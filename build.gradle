plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.20'
    id 'application'
    id "com.diffplug.spotless" version "7.0.0.BETA4"
    id 'org.graalvm.buildtools.native' version '0.10.4'
}

group = 'org.jack'
version = project.version

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.18.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.6.2"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"
    implementation "com.github.ajalt.clikt:clikt:5.0.3"
    implementation "com.github.ajalt.clikt:clikt-markdown:5.0.3"
    implementation "io.github.g0dkar:qrcode-kotlin:4.4.1"
    implementation "com.aallam.ulid:ulid-kotlin:1.3.0"
}

test {
    useJUnitPlatform()
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

kotlin {
    jvmToolchain(21)
}

dependencyLocking {
    lockAllConfigurations()
}

application {
    mainClass = 'org.jack.MainKt'
}

graalvmNative {
    binaries {
        main {
            imageName.set("jack")
            mainClass.set("org.jack.MainKt")
            fallback.set(false)
            buildArgs.add("--enable-url-protocols=https")
        }
    }
    toolchainDetection.set(true)
}

apply from: 'spotless.gradle'

processResources {
    filesMatching('version.properties') {
        expand(project.properties)
    }
}
